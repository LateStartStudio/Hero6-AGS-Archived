// new module script

function game_start () {
  gSaveLoadGame.Visible = false;
  gSaveLoadGame.Centre();
}

/**
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function initSaveLoadGameDialog(bool isSaveGameDialog)
{
  gIconbar.Visible = false;
  if(gOptions.Visible) gOptions.Visible = false;
  mouse.saveCurrentCursor();
  mouse.Mode = eModeInteract;
  
  lstSaveGames.FillSaveGameList();
  lstSaveGames.SelectedIndex = 0;
  if(lstSaveGames.SelectedIndex != -1)
  {
    txtSaveName.Text = lstSaveGames.Items[lstSaveGames.SelectedIndex];
  }
  
  if(isSaveGameDialog)
  {
    txtSaveName.Visible = true;
    btnSave.Visible = true;
    btnLoad.Visible = false;
  }
  else
  {
    txtSaveName.Visible = false;
    btnSave.Visible = false;
    btnLoad.Visible = true;
  }
  gSaveLoadGame.Visible = true;
}

/**
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function cancelSaveLoadGame_OnClick(GUIControl *control, MouseButton button)
{
  mouse.reloadCursor();
  gIconbar.Visible = true;
  gSaveLoadGame.Visible = false;
}

/*
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function save_OnClick(GUIControl *control, MouseButton button)
{
  int totalSaves = lstSaveGames.ItemCount;
  String saveName = txtSaveName.Text;
  
  if(saveName == "")
  {
    Display("Please enter a name for your savegame");
    return;
  }
  
  if(lstSaveGames.SelectedIndex == -1)
  {
    SaveGameSlot(totalSaves+1, saveName);
    cancelSaveLoadGame_OnClick(control, button);
    return;
  }
  else
  {
    int checkSave = 0;
    while(checkSave != totalSaves)
    {
      if(saveName == lstSaveGames.Items[checkSave])
      {
        SaveGameSlot(savegameindex[checkSave], saveName);
        cancelSaveLoadGame_OnClick(control, button);
        return;
      }
      checkSave++;
    }
    SaveGameSlot(totalSaves+1, saveName);
    cancelSaveLoadGame_OnClick(control, button);
  }
}

/**
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function load_OnClick(GUIControl *control, MouseButton button)
{
  int totalLoads = lstSaveGames.ItemCount;
 
  if(lstSaveGames.SelectedIndex == -1)
  {
    Display("Please select a slot to load.");
  }
  else if(lstSaveGames.SelectedIndex >= 0)
  {
    RestoreGameSlot(savegameindex[lstSaveGames.SelectedIndex]);
    cancelSaveLoadGame_OnClick(control, button);
  }
}

/**
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function deleteSaveGame_OnClick(GUIControl *control, MouseButton button)
{
  if(lstSaveGames.SelectedIndex != -1)
  {
    DeleteSaveSlot(savegameindex[lstSaveGames.SelectedIndex]);
    lstSaveGames.FillSaveGameList();
  }
  else
  {
    Display("Please select a save slot to delete.");
  }
}

/**
* http://www.adventuregamestudio.co.uk/wiki/Creating_Custom_Save_and_Load_Dialogs
*/
function saveGames_OnSelectionChange(GUIControl *control)
{
  txtSaveName.Text = lstSaveGames.Items[lstSaveGames.SelectedIndex];
}