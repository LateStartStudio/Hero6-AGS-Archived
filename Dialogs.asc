
function dialog_request(int X) {
	if (X == 2) { // Hywel conversation
		if (flags.DlgHywell == 0) {
			cHywell.Say("Hi. I'm Hywel, the guildmaster here. I suppose you're another one of those heroes who's going to save Albion from the trouble that plagues it.");
			flags.DlgHywell = 1;
		} else {
			cHywell.Say("Hi again. If you want to be helpful, remember to check the questboard on the wall.");
		}
	}	
	if (X == 3) {
		if (flags.DlgVeran == 0) {
			cVeran.Say("Well. Should I even bother to introduce myself?");
			cVeran.Say("What I mean is,");
			cVeran.Say("Do you intend to ignore me entirely,");
			cVeran.Say("Run headlong into the woods,");
			cVeran.Say("Be minced into a bloody mess,");
			cVeran.Say("And trip me during my morning stroll,");
			cVeran.Say("Completely ruining my appetite?");
			cHywell.Say("Belay, man, belay. He just wants to help. Might even want to train with you.");
			cVeran.Say("Introductions then. I'm Veran. And who would you be?");
			Display("You tell Veran your name.");
			cVeran.Say("Well, I hope you will prove more than just another ne'er-do-well glory seeker.");
			flags.DlgVeran = 1;
		} else {
			cVeran.Say("Well, it seems you have managed to remain healthy still. I hope for your sake this is not only luck.");
		}
	}
	if (X == 4) { //initialise duke quest
		if (flags.DukeRescue == 0) flags.DukeRescue = 1;
	}
	if (X == 5) { // seen symbols?
		if (flags.SeenSymbols == true) {
			dColleen01.SetOptionState(8, eOptionOn);
			dColleen02.SetOptionState(8, eOptionOn);
		}
		if (flags.AskedGolemAboutSymbols == true) {
			dColleen01.SetOptionState(11, eOptionOn);
			dColleen02.SetOptionState(11, eOptionOn);
		}
	}
	if (X == 6) {
		Wait(40);
		cEgo.ChangeRoom(11, 130, 133);
	}
	if (X == 7) {
		flags.KnowOfRemanBook = true;
	}	
	if (X == 8) {
		if (flags.Chapter == 1) {
			if (flags.DukeRescue == 5) {
				dRowena01.SetOptionState(11, eOptionOn);
				dRowena01.SetOptionState(9, eOptionOff);
			} else if (flags.DukeRescue < 5 && flags.SeenSymbols) {
				dRowena01.SetOptionState(11, eOptionOff);
				dRowena01.SetOptionState(9, eOptionOn);
			}  
		}
	}
	if (X == 9) {
		if (flags.Chapter == 2 && flags.MobOccuring == 4) {
			cBelenus.Say("Greetings. Have you discovered anything new since our last visit?");
		}
	}
	if (X == 10) {
		if (flags.Chapter == 2 && flags.MobOccuring == 3) {
			flags.MobOccuring = 4;
      
			if (heroStatistics.class == CLASS_FIGHTER) {
				cBelenus.Say("I don't know whether you are strong enough to defeat the Duke's attacker.  Considering the Duke is a paladin, you had best think twice before engaging.");
				cBelenus.Say("Better take preparations just in case. Here are two healing potions");
				cEgo.AddInventory(iHealingPotion);
				cEgo.AddInventory(iHealingPotion);
			} else if (heroStatistics.class == CLASS_MAGE) {
				cBelenus.Say("I sense you do have some magical skill. I don't know whether you will be able to hurt that magical creature with a direct magical attack.");
				cBelenus.Say("Be careful.");
			} else if (heroStatistics.class == CLASS_THIEF) {
				cBelenus.Say("You're a brave man to search for the Duke's attacker. Be careful and keep your distance from that monster.");
				cBelenus.Say("This creature injured the Duke, himself a Paladin. I'm afraid it will be too much for your close combat skills. You had best equip yourself well.");
			}
		}
		cBelenus.Say("If you ever need healing, you know where to come.");
	}	
	if (X == 11) { // buy health potion
		PurchaseItem(iHealingPotion, "You purchase a healing potion.");
	}	
	if (X == 12) { // buy Stamina potion
		PurchaseItem(iStaminaPotion, "You purchase a stamina potion.");
	}		
	if (X == 13) { // buy Mana potion
		PurchaseItem(iManaPotion, "You purchase a mana potion.");
	}	
	
	if (X == 14) {
	  if (flags.DlgGreenleaf == 1)
			dGreenleaf01.SetOptionState(2, eOptionOn);
		else
			dGreenleaf01.SetOptionState(1, eOptionOn);
	  
		if (flags.Chapter == 2 && flags.MobOccuring > 3)
			dGreenleaf01.SetOptionState(13, eOptionOn);
	}
	if (X == 15)
		flags.DlgGreenleaf = 1;
		
	if (X == 16) { // Start Dolmen Quest...
		if (flags.DolmenQuest == 0) {
			flags.DolmenQuest = 1;
		}	
	}	
	if (X == 17) { // Greenleaf dark forest conversation	  
		if (flags.Chapter == 1 || flags.DolmenQuest == 0) {
			cGreenleaf.Say("We dare not speak of it.");
		} else {
			if (!flags.VisitedDarkForest) {
				cGreenleaf.Say("You wish to venture into the dark forest? You won't be able to go far. A mysterious darkness always clouds those regions.");
			} else {
				cGreenleaf.Say("A mysterious darkness always clouds these regions. You noticed this, haven't you?");
			}

			if (flags.GreenleafFavor < GREENLEAF_FAVOR_COMPLETE) {
				cGreenleaf.Say("I can help you get past the barrier of darkness, but first I need to ask a favor of you.");
				dGreenleaf01.SetOptionState(9, eOptionOn);
				dGreenleaf01.SetOptionState(18, eOptionOn);
				dGreenleaf01.SetOptionState(7, eOptionOff);
				dGreenleaf01.SetOptionState(8, eOptionOff);
				dGreenleaf01.SetOptionState(19, eOptionOff);
			} else {
				cGreenleaf.Say("As I've told you before, the only way to see clearly in the dark forest is by drinking the water from this pool just before entering. ");
				cGreenleaf.Say("One bottle should be able to help you for an hour, so I advise you to take as much as you need. ");
				cGreenleaf.Say("Be careful and good luck.");
			}
		}    
	}	
	if (X == 18) { // Greenleaf favor conversation
		if (flags.GreenleafFavor == 0) {
			cGreenleaf.Say("The Lauburu grows in Helmsdale, a ruined village in the dark forest. Why'd you ask?");
			Display("You tell Greenleaf about the Duke's illness, and how you need to find the Lauburu to cure him.");
			cGreenleaf.Say("You will need to get past the dark forests wards to enter Helmsdale. I can make you a potion that will do just that, but I'll need you to bring me a fruit from the cliffside.");
			flags.GreenleafFavor = 1;
			if (heroStatistics.class == CLASS_FIGHTER) {
				cNiall.ChangeRoom(139, 166, 181);
			}
		} else {
			cGreenleaf.Say("As I said before, the Lauburu grows in Helmsdale within the dark forest. I need you to bring me the fruit from the cliffside, then I make you a potion to allow you through the wards of the darkness.");
		}
	}
	if (X == 19) { // show symbol projection
		int a = 99;
		cFernin.LockView(FERNIN_IDLE);
		cFernin.Animate(0, 2, eOnce, eBlock, eForwards);
		cFernin.UnlockView();
		cFernin.Loop = 0;

		cSymbolproj.ChangeRoom(19);
		cSymbolproj.Animate(0, 3, eRepeat, eNoBlock, eForwards);
		while (a > 25) {
			cSymbolproj.Transparency = a;
			a -= 5;
			Wait(2);
		}	
		flags.SeenDarkSymbol = true;
	}	
	if (X == 20) { // fade symbol projection
		int a = 25;
		while (a < 95) {
			cSymbolproj.Transparency = a;
			a += 5;
			Wait(2);
		}
	cSymbolproj.ChangeRoom(-1);
	}
	
	// smith purchase
	if (X == 21) { PurchaseItem(iThrowingDagger, "You purchase a throwing dagger."); }
	if (X == 22) { PurchaseItem(iLongSword, "You purchase a long sword.");           }
	if (X == 23) { PurchaseItem(iChainmail, "You purchase some chainmail.");         }
	if (X == 24) { PurchaseItem(iSteelShield, "You purchase a steel shield.");       }
	if (X == 25) { PurchaseItem(iTinderBox, "You purchase a tinder box.");           }
	if (X == 26) { PurchaseItem(iTorch, "You purchase a torch.");                    }
		
	if (X == 27) { // goto purchase dialog and activate option
		dSmithBuy.SetOptionState(7, eOptionOn);
	}
	if (X == 28) { PurchaseItem(iRations, "You purchase some rations.");             }
	if (X == 29) { PurchaseItem(iFlint, "You purchase a piece of flint.");           }
	
	if (X == 30) {
		flags.DlgTethra = 2;
	}	
	if (X == 32) {
		if (flags.DlgFerninCh2 == 0) {
			cFernin.Say("Welcome to my shop, young adventurer. It is most fortunate and yet unfortunate that you facilitated the return of our duke.");
			cFernin.Say("You shouldn't have moved him, but it's already too late. The damage has been done.");
			cFernin.Say("Still, it might be possible to fix the problem you caused...");
			flags.DlgFerninCh2 = 1;
		} else {
			cFernin.Say("Welcome back adventurer.");
		}
	}
	if (X == 33) {
		if (flags.ThaenIntroduced) {
			dThaen01.SetOptionState(17, eOptionOn);
			dThaen01.SetOptionState(1, eOptionOff);
		} else {
			dThaen01.SetOptionState(17, eOptionOff);
			dThaen01.SetOptionState(1, eOptionOn);
		}
  
		if (flags.SeenCh1JobBoard) {
			dThaen01.SetOptionState(22, eOptionOn);
		} else {
			dThaen01.SetOptionState(22, eOptionOff);
		}
		if (flags.GolemName == 1) {
			dThaen01.SetOptionState(24, eOptionOn);
			dThaen01.SetOptionState(23, eOptionOff);
		} else if (flags.MetMharryon1 && flags.GolemName == 0) {
			dThaen01.SetOptionState(23, eOptionOn);
			dThaen01.SetOptionState(24, eOptionOff);
		}    
	}
	if (X == 34) {
		flags.ThaenIntroduced = true;
	}     
	if (X == 35 && heroStatistics.class == CLASS_MAGE) Display("The sheriff eyes you suspiciously for a few seconds. ");   
	if (X == 36) flags.AskedThaenAboutLibrary = true;  
	if (X == 37) {
		if (flags.AskedThaenAboutLibrary) {
			Display("You SURE you wanna be an adventurer? You didn't know what a library is and you don't even know what a graveyard is?");
			Display("Why don't you try a new line of work? Accounting, maybe?");
		}
	}  
	if (X == 38) { // golem kills you
		GameOverDialog("", "Don't mess with the golem, fool!", false);
	}
	if (X == 39) { // start golem name quest
		if (flags.GolemName == 0) flags.GolemName = 1;
	}
	if (X == 40) { // get names from veran
		if (flags.GolemName < 2) flags.GolemName = 2;
			dVeran.SetOptionState(26, eOptionOn);
			cEgo.AddInventory(iNameList);
	}
	if (X == 41) {
		if (flags.GolemName == 1)
			dVeran.SetOptionState(25, eOptionOn);
		else if (flags.GolemName == 2)
			dVeran.SetOptionState(26, eOptionOn);
	}
	if (X == 42) {
		short a = Random(7);
		if (a == 0) cVeran.Say("Listen to those around you. You never know what fact may save your life.");
		if (a == 1) cVeran.Say("When exploring, be sure you know the quickest way to a place of safety. It wouldn't do to become monster fodder just because you ran the wrong way.");
		if (a == 2) cVeran.Say("Be sure to carry several pieces of food with you at all times, lest you feel the fangs of hunger at the most inopportune moment.");
		if (a == 3) cVeran.Say("Although you have much to do, you should still remember to rest. A tired mind is a slow mind and a tired hand is a weak hand.");
		if (a == 4) cVeran.Say("One man's waste is another man's treasure. Do not discard anything mindlessly. You never know what may be of use to you in the future.");
		if (a == 5) cVeran.Say("Never be ashamed to run from a fight you can't win. There's no glory in getting yourself killed. Of course, if you practice enough, you won't be running very often. I hope.");
		if (a == 6) {
			cVeran.Say("You will someday be asked to fetch magic water. Every adventurer is. Laugh if you must, but this is Natural Law.");
			cVeran.Say("I suggest carrying at least two flasks around at all times. Four if you can afford it. That way, you can always keep a little water for yourself.");
		}
		if (a == 7) cVeran.Say("I've found that when I am particularly perplexed, a visit to the library is refreshing. Even swordsmen must keep their minds engaged.");
	}  
	if (X == 43) {
		cGolem.LockView(GOLEM_BEND);
		cGolem.Animate(0, 2, eOnce, eBlock, eForwards);
		cEgo.AddInventory(iBentSword);
	}	
	if (X == 44) {
		if (flags.ShownBentSwordToRonBars && flags.GolemName == 2) {
			dGlith01.SetOptionState(9, eOptionOn);
		}
	}	
	if (X == 45) {
		cFilter.ChangeRoom(cEgo.Room);
		cFilter.Transparency = 100; //TODO --- Check that the light gives the desired effect
		Wait(1);
		Display("TODO: Wagon cut-scene.");
		Display("Ron bars helps you put the portcullis into the wagon, and then both of you set off to meet with the golem.");
		processTime();//Reset light/dark effect
		flags.GolemName = 3;
		cRonbars.ChangeRoom(137, 180, 211);
		cEgo.Loop = 3;
		cRonbars.Loop = 3;
		cEgo.ChangeRoom(137, 155,210); 
	}	
	if (X == 46) {
		if (flags.SeenSymbols)
			dGolem03.SetOptionState(5, eOptionOn);
		else
			dGolem03.SetOptionState(5, eOptionOff);
		if (cEgo.InventoryQuantity[iDukeSword.ID] == 1)
			dGolem03.SetOptionState(6, eOptionOn);
		else
			dGolem03.SetOptionState(6, eOptionOff);
	}
	if (X == 47) {
		flags.AskedGolemAboutSymbols = true;
	}
	if (X == 48) {
		if (checkmoney(0, 10) == false) {
			Display("You didn't realise inflation on food prices was so high. You simply do not have that kind of money.");
		} else if (flags.NiallDuel == 0) {
			dNiallBerry.SetOptionState(4, eOptionOff);
			dNiallBerry.SetOptionState(7, eOptionOff);
			dNiallBerry.SetOptionState(8, eOptionOn);
			dNiallBerry.SetOptionState(9, eOptionOn);
		} else if (flags.NiallDuel > 0) {
			dNiallBerry2.SetOptionState(2, eOptionOff);
			dNiallBerry2.SetOptionState(3, eOptionOff);
			dNiallBerry2.SetOptionState(4, eOptionOff);
			dNiallBerry2.SetOptionState(5, eOptionOn);
			dNiallBerry2.SetOptionState(6, eOptionOn);
		}    
	}	
	if (X == 49) {
		cNiall.Walk(340, 180, eBlock, eAnywhere);
		cNiall.ChangeRoom(22, 160, 155);
	}	
	if (X == 50) {
		cNiall.Walk(340, 180, eBlock, eAnywhere);
		cNiall.ChangeRoom(-1);
		herobuy(0, 10);
		cEgo.AddInventory(iMagicFruit);
	}
	if (X == 51) {
		cNiall.ChangeRoom(22, 160, 155);
		cEgo.ChangeRoom(22, 145, 165);
	}	
	if (X == 52) {
		if (heroStatistics.currentHealthPoints < (heroStatistics.maxHealthPoints / 4)) {
			cNiall.Say("Seriously, I'm not in the spirit of killing today and you don't look like your fit for a duel. Come back when your ready.");
			return;
		}

		cNiall.Say("Get your gold ready, 'cause you'll be paying me once this is over.");
		if (herobuy(0, 1)) {
			PlayMusic(24);
			cNiall.Say("Engarde!");
			RegisterEnemy(cNiall, 7);
			SpawnEnemyCoords(7, cNiall.x, cNiall.y);
			cNiall.ChangeRoom(-1);
			setCombatDeathMode(false);
			setMovementMode(false);
			InitCombat();
			flags.NiallDuel = 2;
		} else {
			Display("You don't even have a gold coin.");
		}
	}
}