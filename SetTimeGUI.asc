
function initializeSetTime() {
		if (gSetTime.Visible) {		
		gSetTime.Visible = false;
		gIconbar.Visible = true;
		mouse.reloadCursor();
	} else {
		lblDays.Text = String.Format("%d", timeinfo.Day);
		lblHour.Text = String.Format("%d", getHourFromTicks(timeinfo.ticks));
		lblMinutes.Text = String.Format("%d", getMinutesFromTicks(timeinfo.ticks) * 10);
		gSetTime.Visible = true;
		gIconbar.Visible = false;
		mouse.saveCurrentCursor();
		mouse.UseModeGraphic(eModePointer);
	}
}

function acceptSetTime_OnClick() {
	timeinfo.Day = lblDays.Text.AsInt;
	timeinfo.ticks = getTicksFromTime(lblHour.Text.AsInt, lblMinutes.Text.AsInt);
	initializeSetTime();
}

function changeLabelColor_OnHover(Label *labelMostLeft, Label *labelMostRight) {
	GUIControl *currentControl = GUIControl.GetAtScreenXY(mouse.x, mouse.y);
	if(currentControl != null && (currentControl == labelMostLeft || currentControl == labelMostRight)) {
		labelMostLeft.TextColor = 14;
		labelMostRight.TextColor = 14;
	} else {
		labelMostLeft.TextColor = 0;
		labelMostRight.TextColor = 0;
	}
}

function incrementLabelValue() {
	if (GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblDaysDescription || GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblDays) {
		lblDays.Text = String.Format("%d", lblDays.Text.AsInt + 1);
	} else if (GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblHourDescription || GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblHour) {
		if (lblHour.Text.AsInt >= 23) {
			lblHour.Text = "0";
		} else {
			lblHour.Text = String.Format("%d", lblHour.Text.AsInt + 1);
		}
	} else if (GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblMinutesDescription || GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblMinutes) {
		if (lblMinutes.Text.AsInt >= 50) {
			lblMinutes.Text = "0";
		} else {
			lblMinutes.Text = String.Format("%d", lblMinutes.Text.AsInt + 10);
		}
	}
}

function decrementtLabelValue() {
	if (GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblDaysDescription || GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblDays) {
		if (lblDays.Text.AsInt <= 1){
			lblDays.Text = "1";
		} else {
			lblDays.Text = String.Format("%d", lblDays.Text.AsInt - 1);
		}
	} else if (GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblHourDescription || GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblHour) {
		if (lblHour.Text.AsInt <= 0) {
			lblHour.Text = "23";
		} else {
			lblHour.Text = String.Format("%d", lblHour.Text.AsInt - 1);
		}
	} else if (GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblMinutesDescription || GUIControl.GetAtScreenXY(mouse.x, mouse.y) == lblMinutes) {
		if (lblMinutes.Text.AsInt <= 0) {
			lblMinutes.Text = "50";
		} else {
			lblMinutes.Text = String.Format("%d", lblMinutes.Text.AsInt - 10);
		}
	}
}

function game_start () {
	gSetTime.Visible = false;
	gSetTime.Centre();
}

function repeatedly_execute () {
	changeLabelColor_OnHover(lblDaysDescription, lblDays);
	changeLabelColor_OnHover(lblHourDescription, lblHour);
	changeLabelColor_OnHover(lblMinutesDescription,  lblMinutes);
}

function on_key_press (int keycode) {
	if (cEgo.Room != 100 && cEgo.Room != 101 && gSetTime.Visible && (keycode == 124 || keycode == 27)) // | or ESC closes the form
		initializeSetTime();
}

function on_event(EventType event, int data) {
	if (event != eEventGUIMouseDown && !gSetTime.Visible)
		return;
		
	if (mouse.IsButtonDown(eMouseLeft)) // Adjust value up if left click
		incrementLabelValue();
	if (mouse.IsButtonDown(eMouseRight)) // Adjust value up if right click
		decrementtLabelValue();
}

function on_mouse_click (MouseButton button) {
	if(!gSetTime.Visible)
		return;
		
	if (button == eMouseWheelNorth) // Adjust value up if scroll up
		incrementLabelValue();
	if (button == eMouseWheelSouth) // Adjust value down if scroll down
		decrementtLabelValue();
	if (button == eMouseLeft && GUI.GetAtScreenXY(mouse.x, mouse.y) != gSetTime)
		initializeSetTime();
}
