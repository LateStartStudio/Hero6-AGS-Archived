/*  Events  */
function postFade() {
  // script for Room: Player enters room (after fadein)
  /*
if (cEgo.PreviousRoom == 211) {
  cEgo.Walk(cEgo.x,237,eBlock,eAnywhere);
}
*/

  if (flags.GolemName == 3) {
    flags.GolemName = 4;
    flags.AlbionGateFixed = true;
    
    cGolem.Say("You say I fix gate, then you give Reman name? I do it.");
    Display("TODO: The golem picks it up and beings to work on it, bending the steel bars effortlessly.");
    cGolem.Say("Gate now fixed.");
    cRonbars.Say("Amazing. It's as good as new! Thank you. Albion owes both of you a debt.");
    cRonbars.Say("I'll head back to Albion. The sooner the gates are back up, the better.");
    cRonbars.Walk(170, 290, eBlock, eAnywhere);
    cRonbars.ChangeRoom(16, 203, 195);
    cRonbars.Loop = 0;
    
    Display("Ron Bars leaves with the wagon. The golem now looks expectantly at you.");
    //Display("You named the golem Marvin Draíodóir Cosantóir. The golem smiles in appreciation.");
    Display("You named the golem Marvin Draiodoir Cosantoir. The golem smiles in appreciation.");
    cGolem.Say("Me like. Good name. You is friend.");
    cGolem.Say("I let you in cave now. Go find duke.");
    cGolem.Walk(82, 171,eBlock,eWalkableAreas);

  }
}

function preFade() {
  
  if (cGolem.y < 135) {
    cGolem.Transparency = 99;
  }
  StopMusic();
}

function exitNorth() {
  if (cEgo.InventoryQuantity[iTorch.ID] > 0 && cEgo.InventoryQuantity[iFlint.ID] > 0) {
    Display("Using your flint, you strike a light on your torch and enter the cave.");
    cEgo.ChangeRoom(51,160,309);
  }
  else if (cEgo.InventoryQuantity[iTorch.ID] > 0 && cEgo.InventoryQuantity[iSpellFlame.ID] > 0) {
    if (heroinfo.mp >= 5) {
      Display("With a spark from a flame dart spell, your torch is lit and you enter the cave.");
      cEgo.ChangeRoom(51,160,309);
      return;
    }
    else {
      Display("You have a torch but no means to light it. You don't even have sufficient mana to strike a light.");
      return;
    }
    
  }
  else {
    Display("The cave is completely dark. It will be impossible to navigate without some form of light.");
    cEgo.Walk(cEgo.x,  cEgo.y + 10, eBlock, eWalkableAreas);
  }
}

function exitSouth()
{
player.ChangeRoomAutoPosition(211);
}

/*function exitSouth() {
  cEgo.ChangeRoom(211, 160, 305);
}*/


/*  Hotspots  */
function hBush_Look() {
  bush_look();  
}


function hBush_Interact() {
  bush_use();  
}

function hBush_Talk() {
  bush_talk();  
}


function oBentSword_Interact() {
  if (oBentsword.Visible) {
    oBentsword.Visible = false;
    Display("You remove the twisted sword from the ground and take it with you. Let's hope your own sword does not end the same shape.");
    cEgo.AddInventory(iBentSword);
  }
}

function hEntrance_Look()
{
  Display("The cave is very dark.  If there's anything inside, you can't see it from here.");
}

function hEntrance_Interact()
{
  Display("You tentatively reach your hand inside the cave.  Fortunately, nothing bites it off.");
}

function hEntrance_Talk()
{
Display("Hello...hello...hello...");
}

/*  Regions */
function rCaveMouth_WalkOn() {
  if (cGolem.y < 135) {
    cGolem.Transparency = 0;
    cEgo.StopMoving();
    SetRunningMode(0);
    DisplayMessage(7);
    cEgo.LockView(HERO_DOWNHILL);
    cEgo.Walk(cEgo.x,  cEgo.y + 12, eNoBlock, eWalkableAreas);
    cGolem.Walk(130, 162, eBlock, eWalkableAreas);
    cEgo.UnlockView();
    cEgo.Loop = 3;
    dGolem.Start();
  }
}
