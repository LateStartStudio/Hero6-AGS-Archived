//bool notyet = false;
climbStruct myClimb;
bool onBalcony = false;
/* Events */
function postFade() {  // script for Room: Player enters room (after fadein)
  if (flags.Chapter > 0 && flags.MobOccuring != 1) PlayMusic(10);

  if (flags.Chapter == 2 && flags.MobOccuring == 1) {
    DisplayMessage(15);
    cEgo.ChangeRoom(4, 3, 180);
  }
 
}

function preFade() {  
  // put this here so that it doesn't interfere with the guard desc
  if (isDaytime()) oNorthgate.Graphic = 259;
  else oNorthgate.Graphic = 258;
}

function firstEntry(){
}

function onExit(){
}

function repExec(){
}

function exitSouth(){
  player.ChangeRoomAutoPosition(2);
}

function exitWest(){
}

function exitEast(){
  player.ChangeRoom(5, 2, 180);
}

function exitNorth(){
  player.ChangeRoomAutoPosition(11);
}

/* Objects */

/* Hotspots */
function hGateHouse_Look(){
  Display("Two guardposts guard the gate between the city and the castle.");
}

function hGateHouse_Interact(){
  Display("You can't reach them!");
}

function hGateHouse_Talk(){
  Display("You call out to the guard to open the gate.");
  Display("But the guard hasn't been programmed to open the gate yet, so he stands there, and looks at you funny.");
}

function hSheriffOffice_Look(){
  Display("This must be the sheriff's office. The building looks sturdy and austere, fitting for a keeper of the law.");
}

function hSheriffOffice_Interact(){
  //Display("The stones that make up the walls are smooth and cool to the touch. There is nothing out of the ordinary.");
  myClimb.totalClimbPoints = 4;
  myClimb.resetClimbTransitions(myClimb.totalClimbPoints);
  myClimb.minAttemptSkill = 30;
  myClimb.minSucceedSkill = 50;
  myClimb.fallCheckPoint = 1;
  myClimb.fallToX = 76;
  myClimb.fallToY = 208;
  myClimb.fallDamage = 5;
  myClimb.handbarClimbAnimation = false;
  myClimb.messageSkillInsufficient = "The stones making up the walls of the sheriff's office have been fitted together expertly, leaving very few cracks and handholds. You'll need a lot of climbing experience to scale this.";
  myClimb.messageClimbFailed  = "Try as you might, you are unable to make your way up to the balcony.";
  myClimb.messageFall = "You lose your grip and fall painfully to the unforgiving ground, leaving you bruised and battered.";
  //set the climb point dimensions
  myClimb.x[0] = 76;
  myClimb.y[0] = 201;
  myClimb.x[1] = 76;
  myClimb.y[1] = 140;
  myClimb.x[2] = 60;
  myClimb.y[2] = 136;
  myClimb.x[3] = 60;
  myClimb.y[3] = 145;

  //set the transitions as needed
  myClimb.setTrBaseline(0, 165);
  myClimb.setTrBaseline(1, 165);
  myClimb.setTrBaseline(2, 165);
  
  //do the climb
  myClimb.doClimb();
}

function hSheriffOffice_Talk(){
  Display("You consider shouting for the sheriff to come out and face you but quickly abandon the foolish idea. If only you hadn't left your six-shooter in your other pair of trousers...");
}

function hSheriffDoor_Interact(){
  if (flags.Chapter == 2 && flags.MobOccuring == 1) {
    DisplayMessage(524);
    return;
  }
  cEgo.Walk(122, 182,eBlock,eWalkableAreas);

  if (isDaytime()) cEgo.ChangeRoom(15,310,185);
  else Display("The sheriff has locked up tight his office for the night. Nobody will disturb his dreams of justice. NOBODY!");   
}

function hUpperDoor_Look(){
  Display("There is a mysterious door on the top floor of the sheriff's office. You wonder where it might lead.");
}

function hUpperDoor_Interact(){
  if (onBalcony == true)  {
    Display("The door is locked and barred from the inside. There is no way you can open it.");
  }
  else{
    Display("You can't reach the door from down here. You'll have to get closer.");
  }
}

function hUpperDoor_Talk(){
  Display("You try to sweet-talk the door into opening up and revealing its secrets. To your surprise, nothing happens.");
}

function hFlanaginInn_Look(){
  Display("The Inn, just waiting for you to go in.");
}

function hFlanaginInn_Interact(){
  house_use();
}

function hFlanaginInn_Talk(){
  house_talk();
}

function hFlanaginInnDoor_Look(){
  Display("The door to the inn. Mouthwatering smells of cooking escape from small cracks in the wood.");
}

function hFlanaginInnDoor_Talk(){
  Display("You bare your soul into a heartfelt confession, but the door remains unmoved.");
}

function hFlanaginInnDoor_Interact() {
  cEgo.Walk(299, 217,eBlock,eWalkableAreas);
  if (flags.Chapter == 2 && flags.MobOccuring == 1) {
    DisplayMessage(524);
    return;
  }
  //if (isday()) cEgo.ChangeRoom(10,-15,220);
  cEgo.ChangeRoom(10,-15,220); 
}

function hMessageBoard_Look(){
  int totalMessages = 12;
  String boardMessages[12];
  boardMessages[0] = "This board intentionally left blank.[    -The Sheriff.";
  boardMessages[1] = "Curfew begins after sunset.[    -The Sheriff.";
  boardMessages[2] = "Don't leave town alone or unarmed.[    -The Sheriff.";
  boardMessages[3] = "If you see something, say something.[    -The Sheriff.";
  boardMessages[4] = "It is illegal to disrespect the sheriff without the appropriate license (G2577). To obtain said license, inquire within.[    -The Sheriff.";
  boardMessages[5] = "Baker sought for questioning regarding the disappearance of Brownies.[    -The Sheriff.";
  boardMessages[6] = "In the event of another attack, seek shelter immediately.[    -The Sheriff.";
  boardMessages[7] = "Through vigilance, we find security.[Through security, peace.[Through peace, hapiness.[    -The Sheriff.";
  boardMessages[8] = "Report anything out of the ordinary. Or anything extremely into the ordinary.[    -The Sheriff.";
  boardMessages[9] = "SPECIALS:[-Executed eggs[-Judgement jambalaya[-Lawmaker's linguini[-Hanged man's hamburgers[-Punishment pie (peanut, pecan & pistachio)[    -The Sheriff.";
  boardMessages[10] = "The Sherrf is a Big Ol pOOpy Head";
  boardMessages[11] = "UNAUTHORIZED WRITING ON THIS MESSAGE BOARD IS ILLEGAL AND PUNISHABLE BY EXILE, IMPRISONMENT, PUBLIC FLOGGING AND/OR A STERN TALKING TO BY YOUR MOTHER![    -THE SHERIFF.";
  Display("Every day, the sheriff writes a notice or a small piece of advice on this board. Today's message reads:");
  Display(boardMessages[timeinfo.Day%totalMessages]);

}


function hMessageBoard_Talk(){
  Display("You stick your tongue out toward the message board, defiantly mocking the sheriff's oppressive message of conformity and compliance. You are quite the little rebel indeed!");
}

function hMessageBoard_Interact(){
  Display("Messing with the message board would be a quick way to irritate the sheriff and probably land you in jail. So don't.");
}


/* Regions */
function rGateTrigger() {  // script for Region 2: Player walks onto region
}

function rBalconyWalkOff()  {
  myClimb.reverseClimb = false;
  onBalcony = false;
}

function rBalconyWalkOn()  {  //when the character is on the balcony, the climb becomes reversed
  myClimb.reverseClimb = true;
  onBalcony = true;
}