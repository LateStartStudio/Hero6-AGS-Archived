

int a;
bool bentdown = false;

function BeginCutscene() {
  Display("TODO: Nice looking statue cutscene");
  PlayMusic(27);
  Display("As you cautiously approach the mysterious statue...");
  flags.StatueCutSceneSequence = 1;
  SetFadeColor(255, 255, 255);
  gTopbar.Visible = false;
  cEgo.ChangeRoom(30);
}

/*  Events  */
function postFade() {
  bentdown = false;
    
  RemoveWalkableArea(4);
  if (flags.GreenleafFavor != 2 && flags.GreenleafFavor != 3) {
    PlayMusic(9);
  }

  if (flags.GreenleafFavor == 2 && !flags.hadStatueCutscene) {
    oGstatue.Visible = true;
    oGstatue.SetView(GLOWING_STATUE, 0, 0);
    oGstatue.Animate(0, 4, eRepeat, eNoBlock, eForwards);
  }

  if (cEgo.PreviousRoom == 184) cEgo.Walk(212,125,eBlock,eWalkableAreas);
  else if (cEgo.PreviousRoom == 104) {
     cEgo.Animate(0, 3, eOnce, eBlock, eForwards);
     cEgo.UnlockView();
     if (flags.VisitedDarkForest && flags.HadEloiaDream) {
       cGreenleaf.Say("How are you feeling? You should avoid that cursed place until Chapter 2. TODO: Dialog");
     }
  }

  if (flags.GreenleafFavor == 2) {
    flags.GreenleafFavor = 3;
    Display("You return to the pool only to find no-one here.");
    cEgo.FaceLocation(50, 90, eBlock);
    Wait(40);
    Display("Was that statue glowing before?");
  }

  if (flags.GreenleafFavor == 3 && flags.hadStatueCutscene) {
    flags.GreenleafFavor = 4;
    PlayMusic(9);
    RemoveWalkableArea(4);
    SetWalkBehindBase(2, 124);
    Wait(20);
    Display("You wake up.");
    cEgo.Animate(0, 3, eOnce, eBlock, eForwards);
    cEgo.UnlockView();
    //  if (flags.GreenleafFavor < GREENLEAF_FAVOR_COMPLETE) {
    cGreenleaf.Say("You spoke to her?");
    Display("You nod. Greenleaf looks down.");
    cGreenleaf.Say("She has not spoken to anyone but me in a long time.");
    Display("There is silence.");
    CompleteGreenleafFavor();
  }
}


function preFade() {
  if (flags.GreenleafFavor == 2) {
    cGreenleaf.ChangeRoom(-1);
  }
  if (flags.GreenleafFavor == 3 && flags.hadStatueCutscene) {
    cGreenleaf.ChangeRoom(220);
    cEgo.Transparency = 0;
    gTopbar.Visible = true;
    oGstatue.Visible = false;
    cEgo.LockView(HERO_GETUP);
    cEgo.x = 131;
    cEgo.y = 141;
  }

  if (cEgo.PreviousRoom == 104) {
     if (!isDaytime()) {
       timeinfo.Day++;
       timeinfo.ticks = 72;//TODO --- Check if the value gives the desired result
     }
     cEgo.LockView(HERO_GETUP);
  }
}

function firstEntry() {
  RemoveWalkableArea(4);

  if (cEgo.PreviousRoom == 184) {
    if (flags.GreenleafFavor != 2) PlayMusic(9);
    cEgo.Walk(212,125,eBlock,eWalkableAreas);
    Display("As you approach the corner, you feel an overwhelming sense of peace. This is clearly a very magical place.");
  }
  else if (cEgo.PreviousRoom == 104) {
    Display("You awaken to find yourself in a peaceful realm beyond the likes you've ever seen. You are safe now.");
  }
}

function onExit() {
  if (flags.StatueCutSceneSequence != 1) StopMusic();
}

function repExec() {
  a = Random(1200);
  if (a == 1 && !cGreenleaf.Moving) {
    cGreenleaf.UnlockView();
    cGreenleaf.Walk(90, 174, eNoBlock, eWalkableAreas);
    bentdown = false;
  }
  else if (a == 2 && !cGreenleaf.Moving) {
    cGreenleaf.UnlockView();
    cGreenleaf.Walk(235, 182, eNoBlock, eWalkableAreas);
    bentdown = false;
  }
  else if (a == 3 && !cGreenleaf.Moving) {
    cGreenleaf.UnlockView();
    cGreenleaf.Walk(242, 210, eNoBlock, eWalkableAreas);
    bentdown = false;
  }
  else if (a == 4 && !cGreenleaf.Moving) {
    cGreenleaf.UnlockView();
    cGreenleaf.Walk(160, 135, eNoBlock, eWalkableAreas);
    bentdown = false;
  }
  else if (a > 5 && a < 10 && !cGreenleaf.Moving && cGreenleaf.View == GREENLEAF_HAIR) {
    cGreenleaf.LockView(GREENLEAF_HAIR);
    cGreenleaf.Animate(0, 3, eOnce, eNoBlock, eForwards);
  }
  if (cGreenleaf.View == GREENLEAF_HAIR && !cGreenleaf.Animating) {
    cGreenleaf.UnlockView();
  }
  
  if (!bentdown && !cGreenleaf.Moving) {
    bentdown = true;
    cGreenleaf.LockView(GREENLEAF_KNEE);
    cGreenleaf.Animate(0, 2, eOnce, eNoBlock, eForwards);
  }
  if (bentdown && !cGreenleaf.Animating) {
    cGreenleaf.UnlockView();
  }
}

/*  Hotspots  */
function hGroveTree_Look() {
  short descrip = Random(2);
  if (descrip == 0) Display("This tree feeds itself with the fertile water and grassland of the pool.");
  else if (descrip == 1) Display("The tree looks like a reasonable place for climbing practice.");
  else if (descrip == 2) Display("Some squirrels are fighting among each other. You guess this must be a hot living spot.");
}

function hGroveTree_Interact() {
  Display("Climbing this tree is an easy way to improve your climbing skills... if you have any... in the complete game");
}

function hGroveTree_Talk() {
  tree_talk();
}  

function hSmallTree_Look() {
  short descrip = Random(1);
  if (descrip == 0) Display("A small, scrawny tree struggles to grow on the rocky slope.");
  else if (descrip == 1) Display("You feel inspired by this small tree's tenacity and perseverance, growing where it should be impossible to grow.");
}

function hSmallTree_Interact() {
  Display("Even if you could reach the small tree, it would be a shame to mess with it.");
}

function hSmallTree_Talk() {
  tree_talk();
}  


function hCliff_Look() {
  cliff_look();  
}

function hGrass_Look() {
  grass_look();
}

function hGrass_Interact() {
  grass_use();
}

function hGrass_Talk() {
  grass_talk();
}

function hMountain_Look() {
  mountain_look();  
}

function hMountain_Talk() {
  mountain_talk();  
}

function hBoulder_Look() {
  boulder_look();  
}

function hBoulder_Interact() {
  boulder_use();  
}

function hBoulder_Talk() {
  boulder_talk();
}

function hSky_Look() {
  sky_look();  
}

function hSky_Interact() {
  sky_use();  
}

function hSky_Talk() {
  sky_talk();  
}


function hPool_Look()
{
  Display("The crystal clear water gushes from the rocks, weaving a crisp path towards the valley.");
}

function hPool_Inventory()
{
  if (player.ActiveInventory == inventory[18]) {
    cEgo.Walk(204,144,eBlock,eWalkableAreas);
    cEgo.LockView(HERO_PICKUP);
    cEgo.Animate(2,2,eOnce,eBlock,eForwards);
    Display("You fill an empty flask with the water from the pond and take it with you.");
    cEgo.Animate(2,2,eOnce,eBlock,eBackwards);
    cEgo.UnlockView();
  }
}

function hPool_Interact() {
  cEgo.Walk(204,144,eBlock,eWalkableAreas);
  cEgo.LockView(HERO_PICKUP);
  cEgo.Animate(2,2,eOnce,eBlock,eForwards);
  Display("The water is the most refreshing of its kind. You feel an immense satisifaction upon sipping the clear water.");
  flags.CanSeeInDarkForest = true;
  /*
  if (cEgo.InventoryQuantity[18] >= 1) {
		Display("You fill an empty flask with the water from the pond and take it with you.");
		cEgo.LoseInventory(iEmptyFlask);
		cEgo.AddInventory(iFlaskNullWater);
	}
	*/
  cEgo.Animate(2,2,eOnce,eBlock,eBackwards);
  cEgo.UnlockView(); 
} 

function hFlowers_Interact()
{
  Display("You pick one of the magical flowers, but it wilts the moment it is free from the nourishing earth.");
}

function hFlowers_Look() {
  short descrip = Random(4);
  if (descrip == 0) Display("These flowers are very beautiful. They almost seem magical.");
  else if (descrip == 1) Display("They remind you of your Aunt Edna's flowers. Except these ones aren't dead.");
  else if (descrip == 2) Display("Oh look, the bees are hopping from flower to flower. Aren't they cute?");
  else if (descrip == 3) Display("Flowers never were your kind of business. You'd rather pester your little brother.");
  else if (descrip == 4) Display("You see some beautiful flowers near the pond.");
}

function hFlowers_Talk() {
  short descrip = Random(3);
  if (descrip == 0) Display("You try to contact some flowers but they are busy.");
  else if (descrip == 1) Display("You ask for an audience with the queen bee but she refuses. She's busy taking care of the kingdom.");
  else if (descrip == 2) Display("So you've developed a taste for roses?");
  else if (descrip == 3) Display("Ouch, watch out for the thorns!");
}

function hStatue_Interact()
{
  Display("The poor lady has lost her life force long before you even first entered this valley.");
}

function hStatue_Talk()
{
  Display("Practicing your romantic lines, Mr Casanova?");
}

function hStatue_Look() {
  short descrip = Random(3);
  if (descrip == 0) Display("The sculpture depicts a crying woman. A trickle of water runs from her eyes and down her cheeks to the ground, forming a stream which flows into a small pond at the other end of the meadow. There's something mysteriously magical about this statue. Just looking at it fills you with a deep sense of sorrow.");
  else if (descrip == 1) Display("What a work of art!");
  else if (descrip == 2) Display("You wonder who put a statue way up here. Now, if only you knew where 'here' is...");
  else if (descrip == 3) Display("Now that's interesting.");
}

/*  Regions */
function r1_WalkOn() {
  cEgo.ChangeRoom(184,184,310);
}

/*  Objects */
function oStatue_Interact() {
  RestoreWalkableArea(4);
  SetWalkBehindBase(2, 0);
  flags.hadStatueCutscene = true;
  cEgo.Walk(68, 116, eBlock, eWalkableAreas);
  BeginCutscene();
}

